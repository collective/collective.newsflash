*** Settings ***

Resource  plone.txt

Suite Setup  Setup

*** Variables ***

${NEWSFLASH} =  Extra! Extra! Read all about it The Pinball Wizard in a miracle cure!

*** Test cases ***

Test Portlet Removed
    Goto homepage
    Page Should Not Contain  Manage News Flashes

Test New UI
    Goto homepage
    Page Should Contain  You have no newsflash registered

Test Add News Flash
    Goto Homepage
    Click Manage News Flashes
    Add News Flash
    Page Should Contain  ${NEWSFLASH}

Test Remove News Flash
    Goto Homepage
    Click Manage News Flashes
    Remove News Flash
    Page Should Not Contain  ${NEWSFLASH}

*** Keywords ***

Setup
    Log In  admin  admin

Click Manage News Flashes
    Click Element  css=a#manage-newsflashes.link-overlay
    Wait Until Page Contains Element  css=div.pb-ajax div#content-core

Add News Flash
    Click Button  Add
    Wait Until Page Contains Element  css=div#formfield-form-widgets-newsflash-0
    Input Text  css=textarea#form-widgets-newsflash-0  ${NEWSFLASH}
    Click Button  Save
    Wait Until Keyword Succeeds  1  5  Page Should Contain  Latest

Remove News Flash
    Select Checkbox  css=input#form-widgets-newsflash-0-remove
    Click Button  Remove selected
    Wait Until Keyword Succeeds  1  5  Page Should Not Contain Button  Remove selected
    Click Button  Save
    Wait Until Keyword Succeeds  1  5  Page Should Not Contain  Extra! Extra!
