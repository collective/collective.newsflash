*** Settings ***

Resource  keywords.txt
Suite Setup  Start Browser and Log In
Suite Teardown  Close Browser

*** Variables ***

${SITE_OWNER_NAME} =  admin
${SITE_OWNER_PASSWORD} =  secret
${NEWSFLASH} =  Extra! Extra! Read all about it The Pinball Wizard in a miracle cure!

*** Test cases ***

Test News Flash Manager
    Page Should Contain  You have no news flashes registered

    # Test Add News Flash
    Click Manage News Flashes
    Add News Flash
    Page Should Contain  ${NEWSFLASH}

    # Test Remove News Flash
    Click Manage News Flashes
    Remove News Flash
    Page Should Not Contain  ${NEWSFLASH}

*** Keywords ***

Start Browser and Log In
    Open Browser  ${PLONE_URL}
    Log in  ${SITE_OWNER_NAME}  ${SITE_OWNER_PASSWORD}
    Goto Homepage

Click Manage News Flashes
    Click Element  css=#edit-newsflashes.link-overlay
    Wait Until Page Contains Element  css=div.pb-ajax div#content-core

Add News Flash
    Click Button  Add
    Wait Until Page Contains Element  css=div#formfield-form-widgets-newsflash-0
    Input Text  css=textarea#form-widgets-newsflash-0  ${NEWSFLASH}
    Click Button  Save

Remove News Flash
    Select Checkbox  css=input#form-widgets-newsflash-0-remove
    Click Button  Remove selected
    Wait Until Keyword Succeeds  1  5  Page Should Not Contain Button  Remove selected
    Click Button  Save
